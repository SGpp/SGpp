# Copyright (C) 2009 Technische Universitaet Muenchen
# This file is part of the SG++ project. For conditions of distribution and
# use, please see the copyright notice at http://www5.in.tum.de/SGpp

# author Dirk Pflueger (Dirk.Pflueger@in.tum.de), Joerg Blank (blankj@in.tum.de), Alexander Heinecke (Alexander.Heinecke@mytum.de)


Import('env')
Import('src_objs')
Import('moduleDependencies')

src_objs_combined = []

for module in moduleDependencies:
    name = module.lower().split('sg_',1)[1]
    if env[module]:
        src_objs_combined += src_objs[name]

j_env = env.Clone()

# remove compiler warnings caused by swig (as we cannot do anything against them, 
# and as they confuse jenkins):
j_env.AppendUnique(CPPFLAGS=['-w'])

j_env.AppendUnique(SWIGFLAGS=['-c++','-java', '-package', 'sgpp'])
j_env.AppendUnique(CPPPATH=['#/src/sgpp'])
j_env.AppendUnique(CPPFLAGS=['-fno-strict-aliasing'])

jsgpp = j_env.SharedLibrary(target="jsgpp", source = ['jsgpp.i']+src_objs_combined, SHLIBPREFIX = 'lib')

from glob import glob
files = glob('*.java')
env.Install('#/lib/jsgpp/sgpp/', files)

Return('jsgpp')
