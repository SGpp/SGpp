# Copyright (C) 2009 Technische Universitaet Muenchen
# This file is part of the SG++ project. For conditions of distribution and
# use, please see the copyright notice at http://www5.in.tum.de/SGpp

# author Joerg Blank (blankj@in.tum.de), Dirk Pflueger (Dirk.Pflueger@in.tum.de), Alexander Heinecke (Alexander.Heinecke@mytum.de), David Pfander (David.Pfander@ipvs.uni-stuttgart.de)


srcs = Split("""
pysgpp.i
""")

Import('env')
Import('src_objs')
Import('moduleList')

src_objs_combined = []

for module in moduleList:
    name = module.lower().split('sg_',1)[1]
    if env[module]:
        src_objs_combined += src_objs[name]


py_env = env.Clone()

# remove compiler warnings caused by swig (as we cannot do anything against them, 
# and as they confuse jenkins):
py_env.AppendUnique(CPPFLAGS=['-w'])
if env['PLATFORM']=='cygwin':
    py_env.Append(LIBS=['python2.7'])    
elif env['PLATFORM']=='darwin':
    py_env['SHLIBSUFFIX'] = '.so'

py_env.AppendUnique(SWIGFLAGS=['-c++','-python'] + ['-D'+c for c in py_env['CPPDEFINES']], CPPPATH=['#/src/sgpp'])

# The explicit ".so" suffix is required for OSX as otherwise ".dylib" is used in OSX. Python however requires ".so" on OSX as well. On all other plattforms ".so" actually is the default.
pysgpp = py_env.SharedLibrary(target="pysgpp", source = src_objs_combined+srcs, SHLIBPREFIX = '_')

Export('pysgpp')
Return('pysgpp')
