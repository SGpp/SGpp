# Copyright (C) 2008-today The SG++ project
# This file is part of the SG++ project. For conditions of distribution and
# use, please see the copyright notice provided with SG++ or at 
# sgpp.sparsegrids.org

import os
import fnmatch

Import('env')
Import('BUILD_DIR')
Import('moduleName')

libs = ['sgppbase', 'sgpppde', 'sgppfinance','sgppsolver', 'sgppdatadriven']

srcs = []
for currentFolder, subdirNames, fileNames in os.walk("."):
    if currentFolder.startswith("./src"):
        for fileName in fnmatch.filter(fileNames, '*.cpp'):
            srcs.append(os.path.join(currentFolder, fileName))

objs = []
for sourceFile in srcs:
    objs.append(env.SharedObject(sourceFile))

lib = env.SharedLibrary(target="sgpp" + moduleName,
                        source=objs,
                        LIBPATH=BUILD_DIR,
                        LIBS=libs)
                        
env.Depends(lib, "#/" + BUILD_DIR.path + "/" + env["LIBPREFIX"] + "sgppbase" + env["SHLIBSUFFIX"])                        
env.Depends(lib, "#/" + BUILD_DIR.path + "/" + env["LIBPREFIX"] + "sgpppde" + env["SHLIBSUFFIX"])
env.Depends(lib, "#/" + BUILD_DIR.path + "/" + env["LIBPREFIX"] + "sgppdatadriven" + env["SHLIBSUFFIX"])
env.Depends(lib, "#/" + BUILD_DIR.path + "/" + env["LIBPREFIX"] + "sgppfinance" + env["SHLIBSUFFIX"])
env.Depends(lib, "#/" + BUILD_DIR.path + "/" + env["LIBPREFIX"] + "sgppsolver" + env["SHLIBSUFFIX"])
libInstall = env.Install(BUILD_DIR, lib)

if not env['NO_UNIT_TESTS'] and env['SG_PYTHON']:
  Import('testTargetList')
  moduleTest = env.Test(os.path.join('tests', 'test_%s.py' % moduleName))
  env.Requires(moduleTest, libInstall)
  testTargetList.append(moduleTest)

Import('exampleTargetList')
for fileName in os.listdir("examples"):
    if fnmatch.fnmatch(fileName, "*.cpp"):
        example = env.Program(source=os.path.join("examples", fileName),
                              LIBPATH=BUILD_DIR,
                              LIBS=libs + ["sgpp%s" % moduleName])
        env.Requires(example, libInstall)
        exampleTargetList.append(example)

# build unit tests
if env['COMPILE_BOOST_TESTS']:
    Import('boostTestTargetList')
    test_env = env.Clone()
    test_env.AppendUnique(LIBPATH=[BUILD_DIR])
    test_env.AppendUnique(LIBS=libs + ["sgpp%s" % moduleName, "boost_unit_test_framework", "z"])
    testObjects = []
    for fileName in os.listdir("tests"):
        if fnmatch.fnmatch(fileName, "*.cpp"):
            testObjects += [test_env.Object(source=os.path.join("tests", fileName))]
    if len(testObjects) > 0:
        fileName = os.path.join("tests", "test_{}_boost".format(moduleName))
        test = test_env.Program(fileName, testObjects)
        test_env.Requires(test, libInstall)
        if env['RUN_BOOST_TESTS']:
            testRun = test_env.BoostTest(fileName + "_run", source=fileName)
            boostTestTargetList.append(testRun)
            
Import('libraryTargetList')
Import('installTargetList')
libraryTargetList.append(lib)
installTargetList.append(libInstall)