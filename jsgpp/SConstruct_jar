# Copyright (C) 2008-today The SG++ project
# This file is part of the SG++ project. For conditions of distribution and
# use, please see the copyright notice provided with SG++ or at
# sgpp.sparsegrids.org

# This file is needed for compiling the *.java files produced by the main
# SConstruct to *.class files and packing the classes into a *.jar file.
# SCons cannot do this in the same SConstruct which runs SWIG to produce
# the *.java files, as SCons cannot know the names of the *.java files
# which SWIG produces (the SharedLibrary builder invoking SWIG only returns
# the .so as target). As a result, SCons doesn't see the *.java files in
# its processing phase and calls javac with all the *.java files missing.
# Therefore, we have to do the javac compilation in a separate SCons process.

import fnmatch
import os.path

vars = Variables("custom.py")
vars.Add("JSGPP_BUILD_PATH", "E.g. /path/to/SGpp/trunk/lib/jsgpp.",
         os.path.join("..", "lib", "jsgpp"))
j_env = Environment(variables=vars, ENV=os.environ)

JSGPP_BUILD_PATH = Dir(j_env["JSGPP_BUILD_PATH"])

# clear java/sgpp/*.class files
if os.path.exists(os.path.join("java", "sgpp")):
    for filename in os.listdir(os.path.join("java", "sgpp")):
        if fnmatch.fnmatch(filename, "*.class"):
            os.remove(os.path.join("java", "sgpp", filename))

javaClasses = j_env.Java(target="java",
                         source=["java", "LoadJSGPPLib.java"])

jar = j_env.Jar(target="jsgpp.jar",
                source=os.path.join("java", "sgpp"),
                JARCHDIR="java")
j_env.Depends(jar, javaClasses)

# Don't forget the Default call, as without SCons doesn't copy the *.jar file
# to JSGPP_BUILD_PATH since it's outside of the current directory
# (which should be /path/to/SGpp/trunk/jsgpp) and per default,
# SCons only builds targets within the current directory.
# Of course, the SCons documentation doesn't point out that,
# which is at least a bug in the documentation, if not in SCons itself.
# (The FAQ suggests calling SCons with "scons /" to build all targets
# under the root directory... Argh. Really? :/ )
j_env.Default(j_env.Install(JSGPP_BUILD_PATH, jar))
