# Copyright (C) 2008-today The SG++ project
# This file is part of the SG++ project. For conditions of distribution and
# use, please see the copyright notice provided with SG++ or at
# sgpp.sparsegrids.org

import os

Import("*")

# as the dependency tracking for swig is buggy in scons,
# always trigger a "reswig" by removing the swig wrapper
if os.path.exists("matsgpp_wrap.cc"):
  os.remove("matsgpp_wrap.cc")

matlabEnv = env.Clone()

# add C++ defines for all modules
for module in moduleNames:
  if matlabEnv[module]:
    matlabEnv["CPPDEFINES"][module] = "1"

# remove compiler warnings caused by swig
# (as we cannot do anything against them and as they confuse jenkins)
matlabEnv.AppendUnique(CPPFLAGS=["-w"])

defines = ["-D" + c + "=" + matlabEnv["CPPDEFINES"][c]
           for c in matlabEnv["CPPDEFINES"]]
#swigIncludePaths = ["-I" + module + "/src/" for module in moduleFolders]
swigIncludePaths = []
matlabEnv.AppendUnique(SWIGFLAGS=["-c++", "-matlab", "-mexname", "matsgppmex",
                                  "-outdir", os.path.join(moduleName, "matlab")] +
                                  swigIncludePaths + defines)

#matlabEnv["STATIC_AND_SHARED_OBJECTS_ARE_THE_SAME"]=1

matlabEnv.AppendUnique(LIBPATH=BUILD_DIR)
matlabEnv.AppendUnique(LIBS=flattenedDependencyGraph)

if env["USE_GSL"]:
  matlabEnv.AppendUnique(LIBS=["gsl", "gslcblas"])

if env["USE_ZLIB"]:
  matlabEnv.AppendUnique(LIBS=["z"])

matlabEnv.AppendUnique(CPPPATH=matlabEnv["MATLAB_INCLUDE_PATH"])
matlabEnv.AppendUnique(LIBPATH=matlabEnv["MATLAB_LIBRARY_PATH"])
matlabEnv.AppendUnique(LIBS=["mex", "mx", "mat"])

if env["PLATFORM"] == "win32":
  mexSuffix = "mex.mexw64"
elif env["PLATFORM"] == "darwin":
  mexSuffix = "mex.mexmaci64"
else:
  mexSuffix = "mex.mexa64"

libMatlab = matlabEnv.SharedLibrary(target="matsgpp", source=["matsgpp.i"],
                                    SHLIBPREFIX="", SHLIBSUFFIX=mexSuffix)

libraryTargetList.append(matlabEnv.Install(MATSGPP_BUILD_PATH, libMatlab))

for file in ["+matsgpp", "SwigGet.m", "SwigMem.m", "SwigRef.m", "SwigStorage.m"]:
	libraryTargetList.append(
        matlabEnv.Install(MATSGPP_BUILD_PATH, os.path.join("matlab", file)))
